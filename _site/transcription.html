<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.1" exclude-result-prefixes="tei"
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
	xmlns:tei="http://www.tei-c.org/ns/1.0">
	<xsl:output method="html" version="4.01" encoding="utf-8" indent="yes" doctype-system="http://www.w3.org/TR/html4/strict.dtd" doctype-public="-//W3C//DTD HTML 4.01//EN" />
	
	<xsl:template match="/">
		<html>
			<head>
<meta http-equiv="content-type" content="text/html; charset=utf-8" />
<title>Seville</title>
<meta name="keywords" content="" />
<meta name="description" content="" />
<link href="default.css" rel="stylesheet" type="text/css" />
</head>
<body>
<div id="header">
	<h1><xsl:value-of select="//tei:title"/></h1>
	<h2>A Text from the Fourteenth Century</h2>
</div>
<div id="menu">
	<ul>
		<li><a href="home.html">Home</a> </li>
		<li><a href="transcriptions.html">Transcriptions</a></li>
		<li><a
			href="translation.html">Translation</a></li>
		<li><a href="files.html">Files</a></li>
		<li><a href="about.html">About</a> </li>
		<li><a href="http://www.arsmusicae.org/wordpress/">Blog</a> </li>
	</ul>
</div>
<div id="content">
	
	<div id="left">
		<h2><xsl:value-of select="//tei:settlement"/><xsl:text>, </xsl:text>
			<xsl:value-of select="//tei:repository"/><xsl:text>, </xsl:text>
			<xsl:value-of select="//tei:idno"/></h2>
		<xsl:apply-templates/>
		<div id="footer">
			<xsl:call-template name="footnote"/>
		</div>
	</div>
	<div id="right">
		<h2>Images<a
			href="sevilleImageViewer.htm" target="_new">Launch viewer</a></h2>
		<xsl:call-template name="leftImage"/>
	</div>
</div>
<div id="footer">
	<p> Copyright @Creative Commons 2011. Designed by <a href="http://www.freecsstemplates.org" class="link1">Free CSS Templates</a></p>
</div>
</body>
		</html>
		
	</xsl:template>
	
	<xsl:template match="//tei:fileDesc" />
	
	<xsl:template match="//tei:encodingDesc" />
	
	<xsl:template match="//tei:front" />
	
	<xsl:template match="tei:p">
		<xsl:variable name="id"><xsl:number level="any" from="tei:text"/></xsl:variable>
		<p id="{@xml:id}" class="plaoulparagraph"><a name="{$id}" class="paragraphnumber"><xsl:number level="any" from="tei:text"/></a><xsl:apply-templates/><span class="paragraphmenu"><a class="paragraphmenu">Paragraph Menu</a></span></p><div class="paradiv" id="{@xml:id}" style="display: none;"></div>
	</xsl:template>
	
	<xsl:template match="div"/>
	
	<xsl:template match="tei:lb">
		<br/><xsl:apply-templates/>
	</xsl:template>
	
	<xsl:template match="tei:pb">
		<hr/>[folio <xsl:value-of select="@n"/>]<br/>
		<xsl:apply-templates/>
	</xsl:template>
	
	<xsl:template name="leftImage">
		<xsl:for-each select="//tei:pb">
			<hr/>[folio <xsl:value-of select="@n"/>]<br/><img src="{@facs}" />
			<xsl:apply-templates/>
		</xsl:for-each>
	</xsl:template>
	
	<xsl:template match="tei:hi">
		<span style="color:#cd5c5c;font-weight:bold;font-size: 150%"><xsl:apply-templates/></span>
	</xsl:template>
	
	<xsl:template match="tei:ref">
		<a href="{@target}"><xsl:apply-templates/></a>
	</xsl:template>
	
	<xsl:template match="tei:corr">
		<xsl:text> </xsl:text><span class="emendation">[<em>corr.</em>
			<xsl:text> </xsl:text><xsl:value-of select="."/>]<xsl:text> </xsl:text></span>
	</xsl:template>
	
	<xsl:template match="tei:note">
		<xsl:variable name="id"><xsl:number count="//tei:note" level="any" format="a"/></xsl:variable>
		<sup><a href="#footnote{$id}" name="footnotereference{$id}" class="footnote"><xsl:copy-of select="$id"/></a></sup>
	</xsl:template>
	
	<xsl:template name="footnote">
		<ul><xsl:for-each select="//tei:note">
			<xsl:variable name="id"><xsl:number count="//tei:note" level="any" format="a"/></xsl:variable>
			<li id="footnote{$id}"><a href="#footnotereference{$id}"><xsl:copy-of select="$id"/></a> -- <xsl:apply-templates/></li>
		</xsl:for-each>
		</ul>
	</xsl:template>
	
</xsl:stylesheet>
